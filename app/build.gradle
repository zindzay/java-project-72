plugins {
    id 'application'
    id 'checkstyle'
    id 'jacoco'
    id "com.adarshr.test-logger" version "3.2.0"
    id 'se.patrikerdes.use-latest-versions' version '0.2.18'
    id 'com.github.ben-manes.versions' version '0.46.0'
    id 'net.ltgt.errorprone' version '3.0.1'
    id('io.ebean') version '13.6.5'
}

group 'hexlet.code'
version '1.0-SNAPSHOT'

application {
    mainClass = 'hexlet.code.App'
}

repositories {
    mavenCentral()
}

dependencies {
    implementation(
            'io.javalin:javalin:5.4.2',
            'io.javalin:javalin-rendering:5.4.2',
            'org.slf4j:slf4j-simple:2.0.5',

            'org.thymeleaf:thymeleaf:3.1.1.RELEASE',
            'nz.net.ultraq.thymeleaf:thymeleaf-layout-dialect:3.2.0',
            'org.thymeleaf.extras:thymeleaf-extras-java8time:3.0.4.RELEASE',
            'org.webjars:bootstrap:5.2.3',

            'io.ebean:ebean:13.15.0',
            'io.ebean:ebean-querybean:13.15.0',
            'io.ebean:ebean-annotation:8.3',
            'io.ebean:ebean-ddl-generator:13.15.0',
            'io.ebean:ebean-migration:13.7.0',

            // NOTE: ebean-ddl-generator требует старые зависимости, которых нет в java 9 и выше
            'org.glassfish.jaxb:jaxb-runtime:2.3.5',
            'javax.activation:activation:1.1.1',

            'com.h2database:h2:2.1.214',
            'org.postgresql:postgresql:42.5.4',

            'com.konghq:unirest-java:3.14.2',
    )

    annotationProcessor 'io.ebean:querybean-generator:13.15.0'

    testImplementation(
            'org.junit.jupiter:junit-jupiter-api:5.9.2',
            'org.junit.jupiter:junit-jupiter-params:5.9.2',
            'org.assertj:assertj-core:3.24.2',
            'org.slf4j:slf4j-log4j12:2.0.7',
            'io.ebean:ebean-test:13.15.0',
            'com.squareup.okhttp3:mockwebserver:4.10.0'
    )

    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.9.2'

    errorprone 'com.google.errorprone:error_prone_core:2.18.0'
}

compileJava {
    options.release.set(17)
    options.encoding = 'UTF-8'
}

test {
    useJUnitPlatform()
}

testlogger {
    showStandardStreams true
}

jacocoTestReport {
    reports {
        xml.required = true
    }
}

checkstyle {
    toolVersion '9.2.1'
}

task stage(dependsOn: ['clean', 'installDist'])
installDist.mustRunAfter clean

task generateMigrations(type: JavaExec) {
    classpath = sourceSets.main.runtimeClasspath
    mainClass.set('hexlet.code.MigrationGenerator')
}
